# Maintainer: okayurisotto <47853651+okayurisotto@users.noreply.github.com>
pkgname=ttf-plemoljp
pkgver=1.2.4
pkgrel=2
pkgdesc='A composite font of IBM Plex Mono and IBM Plex Sans JP.'
arch=('any')
url='https://github.com/yuru7/PlemolJP'
license=('custom')
provides=('ttf-font-nerd')
groups=('modified')
source=("${url}/releases/download/v${pkgver}/PlemolJP_HS_v${pkgver}.zip"
        "${url}/releases/download/v${pkgver}/PlemolJP_NF_v${pkgver}.zip"
        "${url}/releases/download/v${pkgver}/PlemolJP_v${pkgver}.zip"
        "https://raw.githubusercontent.com/yuru7/PlemolJP/v${pkgver}/LICENSE")
sha256sums=('e2399754b087409fe8d4f6ceeeb8bb1d68c835ba88347899fd695b59cf1c1e12'
            '55cb3609dc7fc0afa15d8701451dcb0c2e687d64a955e687d874647248d4029f'
            'a960536fffe1b5c7bc4aeb416fd82161ee4822b3b5f69525af9abbdb652df8c9'
            '52bbb5e729acc62435831d20641ece6a919e610100285ba183ef4d7233fb1e9a')

package() {
	find "${srcdir}" \
		-type f \
		-name '*.ttf' \
		-exec install -Dm644 {} -t "${pkgdir}/usr/share/fonts/TTF" \;

	install -Dm644 "${srcdir}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
